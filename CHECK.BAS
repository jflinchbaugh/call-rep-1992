   10 SCREEN 0:WIDTH 80:VIEW PRINT:COLOR 4,0,0:KEY OFF:CLS:LNGTH=104
   15 KEY(10) ON:ON KEY(10) GOSUB 30000
   20 ON ERROR GOTO 10000
   30 LOCATE 1,31:PRINT "CallChecker v1.7";
   35 LOCATE 2,33:COLOR 1:PRINT "<F10> TO EXIT";
   40 VIEW PRINT 3 TO 24:COLOR 7
   50 INPUT "YEAR(YYYY): ",Y:IF Y<1985 THEN GOTO 50
   60 FILE$=RIGHT$(STR$(Y),4)+".FCR"
   70 OPEN FILE$ AS 1 LEN=LNGTH
   80 FIELD 1,6 AS DF$,13 AS RAF$,1 AS CDF$,35 AS LF$,20 AS NF$,4 AS TOF$,4 AS TIF$,2 AS MF$,6 AS PDF$,2 AS PF$,1 AS TF$,1 AS KF$,1 AS S16F$,1 AS S21F$,1 AS S34F$,1 AS S35F$,1 AS S36F$,1 AS S41F$,1 AS S42F$,1 AS S44F$,1 AS SOF$
   85 IF LOF(1)=0 THEN GOSUB 20000
   90 CLS
  100 LPRINT "CALL NUMBERS:";
  110 FOR CN=1 TO LOF(1)/LNGTH
  115 LOCATE 12,1:PRINT "CALL";CN
  120 GET 1,CN
  125 'DATE
  130 M=VAL(LEFT$(DF$,2))
  140 IF M<1 OR M>12 THEN LPRINT CN;"-DATE";
  150 D=VAL(MID$(DF$,3,2))
  160 IF D<1 OR D>31 THEN LPRINT CN;"-DATE";
  170 Y=VAL(RIGHT$(DF$,2))
  180 IF Y<>VAL(MID$(FILE$,3,2)) THEN LPRINT CN;"-DATE";
  190 'RESPONSE AREA
  200 IF LEFT$(RAF$,13)="LOWER WINDSOR" THEN GOTO 270
  210 IF LEFT$(RAF$,7)="WINDSOR" THEN GOTO 270
  220 IF LEFT$(RAF$,12)="YORKANA BORO" THEN GOTO 270
  230 IF LEFT$(RAF$,11)="HELLAM BORO" THEN GOTO 270
  240 IF LEFT$(RAF$,6)="HELLAM" THEN GOTO 270
  250 IF LEFT$(RAF$,6)="OTHERS" THEN GOTO 270
  260 LPRINT CN;"-RESPONSE AREA";
  270 'DESIGNATION
  280 IF CDF$<>"F" AND CDF$<>"M" THEN LPRINT CN;"-DESIGNATION";
  290 'TIMES
  300 H=VAL(LEFT$(TOF$,2))
  310 IF H<0 OR H>23 THEN LPRINT CN;"-TIME OUT";
  320 M=VAL(RIGHT$(TOF$,2))
  330 IF M<0 OR M>59 THEN LPRINT CN;"-TIME OUT";
  340 H=VAL(LEFT$(TIF$,2))
  350 IF H<0 OR H>23 THEN LPRINT CN;"-TIME IN";
  360 M=VAL(RIGHT$(TOF$,2))
  370 IF M<0 OR M>59 THEN LPRINT CN;"-TIME IN";
  380 'DISTANCE
  390 M=VAL(MF$)
  400 IF M<0 OR M>99 THEN LPRINT CN;"-MILES";
  410 'DAMAGE
  420 D=VAL(PDF$)
  430 IF D<0 THEN LPRINT CN;"-DAMAGE";
  440 'PERSONNEL
  450 P=VAL(PF$)
  460 IF P<0 OR P>99 THEN LPRINT CN;"-PERSONNEL";
  470 'TRUCKS
  480 T=VAL(TF$)
  490 IF T<0 OR T>4 THEN LPRINT CN;"-TRUCKS";
  500 'DEATHS
  510 K=VAL(KF$)
  520 IF K<0 OR K>9 THEN LPRINT CN;"-DEATHS";
  530 'MUTUAL AIDS
  540 IF ASC(S16F$)<>0 AND ASC(S16F$)<>32 THEN LSET S16F$="*"
  550 IF ASC(S21F$)<>0 AND ASC(S21F$)<>32 THEN LSET S21F$="*"
  560 IF ASC(S34F$)<>0 AND ASC(S34F$)<>32 THEN LSET S34F$="*"
  570 IF ASC(S35F$)<>0 AND ASC(S35F$)<>32 THEN LSET S35F$="*"
  580 IF ASC(S36F$)<>0 AND ASC(S36F$)<>32 THEN LSET S36F$="*"
  590 IF ASC(S41F$)<>0 AND ASC(S41F$)<>32 THEN LSET S41F$="*"
  600 IF ASC(S42F$)<>0 AND ASC(S42F$)<>32 THEN LSET S42F$="*"
  610 IF ASC(S44F$)<>0 AND ASC(S44F$)<>32 THEN LSET S44F$="*"
  620 IF ASC(SOF$)<>0 AND ASC(SOF$)<>32 THEN LSET SOF$="*"
  630 PUT 1,CN
  640 NEXT CN
  645 CLOSE
  650 GOTO 10
10000 IF ERR=25 THEN LOCATE 12,1:PRINT "PRINTER FAULT.  FIX IT AND PRESS <ENTER>" :ELSE RESUME 0
10010 WHILE+ INKEY$<>CHR$(13):WEND
10020 RESUME
20000 LOCATE 12,1:PRINT "THIS YEAR IS NOT RECORDED! <ENTER>";
20010 CLOSE:KILL FILE$
20020 WHILE+ INKEY$<>CHR$(13):WEND
20030 RETURN 10
30000 CLS:RUN"MENU.BAS"
