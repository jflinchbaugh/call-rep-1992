10 SCREEN 0:WIDTH 80:VIEW PRINT:COLOR 4,0,0:LOCATE ,,,0,7:KEY OFF:CLS:LNGTH=104
20 KEY(10) ON:ON KEY(10) GOSUB 440
30 LOCATE 1,32:PRINT "CALL INFO v1.7";
40 VIEW PRINT 2 TO 24:COLOR 7
50 INPUT "YEAR(YYYY): ",Y:IF Y<1985 THEN GOTO 50
52 IF Y=VAL(RIGHT$(DATE$,4)) THEN GOTO 60
53 IF Y=VAL(RIGHT$(DATE$,4))-1 AND VAL(LEFT$(DATE$,2))=1 THEN GOTO 60
55 PRINT "IS THIS YEAR CORRECT (Y/N)?";:BEEP
56 FOR X=1 TO 300:A$=INKEY$:NEXT X
57 A$=INKEY$:IF A$="N" THEN GOTO 10 ELSE IF A$<>"Y" THEN GOTO 57
60 FILE$=RIGHT$(STR$(Y),4)+".FCR"
70 OPEN FILE$ AS 1 LEN=LNGTH
80 FIELD 1,6 AS DF$,13 AS RAF$,1 AS CDF$,35 AS LF$,20 AS NF$,4 AS TOF$,4 AS TIF$,2 AS MF$,6 AS PDF$,2 AS PF$,1 AS TF$,1 AS KF$,1 AS S16F$,1 AS S21F$,1 AS S34F$,1 AS S35F$,1 AS S36F$,1 AS S41F$,1 AS S42F$,1 AS S44F$,1 AS SOF$
90 CLS
100 RESTORE 500
110 FOR X=1 TO 6:READ X$:KEY X,X$:NEXT
120 LOCATE 2,1:KEY LIST:COLOR 12:LOCATE 5,40:PRINT "F10 TO EXIT AND SAVE.";:VIEW PRINT 8 TO 25:CLS
130 CLS:LOCATE 25,1:COLOR 12:PRINT "PREV CALL:";:COLOR 15:PRINT LOC(1);:COLOR 12:PRINT TAB(32);"TOTAL CALLS:";:COLOR 7:PRINT CINT(LOF(1)/LNGTH);:COLOR 12:PRINT TAB(69);"YEAR: ";:COLOR 8:PRINT LEFT$(FILE$,4);
140 LOCATE 8,1:COLOR 9:INPUT "CALL NUMBER: ",CN:IF CN>LOF(1)/LNGTH+1 THEN COLOR 12:PRINT "INVALID CALL!":FOR X=1 TO 3000:NEXT:LOCATE 9,1:PRINT SPC(20);:GOTO 140
150 IF CN<=0 THEN CN=LOF(1)/LNGTH+1
160 GET 1,CN
170 CLS:GOSUB 250
180 IF CN<=LOF(1)/LNGTH THEN GOSUB 290
190 GOSUB 600:CLS:GOSUB 460
200 IF CN<=LOF(1)/LNGTH THEN GOSUB 370
210 GOSUB 670:CLS:GOSUB 760
220 IF CN<=LOF(1)/LNGTH THEN GOSUB 810
230 GOSUB 920
235 IF ASC(DF$)<>0 THEN PUT 1,CN
240 GOTO 130
250 RESTORE 510
260 LOCATE 25,1:COLOR 12:PRINT "THIS CALL:";:COLOR 15:PRINT LOC(1);:COLOR 12:PRINT TAB(32);"TOTAL CALLS:";:COLOR 7:PRINT CINT(LOF(1)/LNGTH);:COLOR 12:PRINT TAB(69);"YEAR: ";:COLOR 8:PRINT LEFT$(FILE$,4);
270 FOR X=1 TO 6:READ FLD$,L:LOCATE X*2+6,1:COLOR 3:PRINT FLD$;:LOCATE X*2+7,30:COLOR 14:PRINT STRING$(L,24);:NEXT X
280 'RETURN
290 'GET 1,CN
300 LOCATE 8,30:COLOR 6:PRINT DF$;
310 LOCATE 10,30:PRINT RAF$;
320 LOCATE 12,30:PRINT CDF$;
330 LOCATE 14,30:PRINT LF$;
340 LOCATE 16,30:PRINT NF$;
350 LOCATE 18,30:PRINT TOF$;
360 RETURN
370 LOCATE 8,30:COLOR 6:PRINT TIF$;
380 LOCATE 10,30:PRINT MF$;
390 LOCATE 12,30:PRINT PDF$;
400 LOCATE 14,30:PRINT PF$;
410 LOCATE 16,30:PRINT TF$;
420 LOCATE 18,30:PRINT KF$;
430 RETURN
440 'LEAVE
450 CLOSE:GOSUB 740:LOCATE ,,,6,7:VIEW PRINT:RUN"MENU.BAS"
460 RESTORE 540
470 LOCATE 25,1:COLOR 12:PRINT "THIS CALL:";:COLOR 15:PRINT LOC(1);:COLOR 12:PRINT TAB(32);"TOTAL CALLS:";:COLOR 7:PRINT CINT(LOF(1)/LNGTH);:COLOR 12:PRINT TAB(69);"YEAR: ";:COLOR 8:PRINT LEFT$(FILE$,4);
480 FOR X=1 TO 6:READ FLD$,L:LOCATE X*2+6,1:COLOR 3:PRINT FLD$;:LOCATE X*2+7,30:COLOR 14:PRINT STRING$(L,24);:NEXT X
490 RETURN
500 DATA LOWER WINDSOR,WINDSOR,YORKANA BORO,HELLAM BORO,HELLAM,OTHERS
510 DATA "1. DATE (MMDDYY):",6,"2. RESP. AREA (F1-F6):",13
520 DATA "3. CALL DESIGNATION (F/M):",1,"4. LOCATION (35):",35
530 DATA "5. NATURE (20):",20,"6. TIME OUT (HHMM):",4
540 DATA "7. TIME IN (HHMM):",4,"8. MILES (2):",2
550 DATA "9. DAMAGE (6):",6,"10. PERSONNEL (2):",2,"11. TRUCKS (1-4):",1
560 DATA "12. DEATHS (1):",1
570 DATA "13. MUTUAL STA. 16:","14. MUTUAL STA. 21:","15. MUTUAL STA. 34:"
580 DATA "16. MUTUAL STA. 35:","17. MUTUAL STA. 36:","18. MUTUAL STA. 41:"
590 DATA "19. MUTUAL STA. 42:","20. MUTUAL STA. 44:","21. OTHER MUTUAL STA.:"
600 COLOR 6:LOCATE 8,30:INPUT "",D$:IF D$<>"" AND LEN(D$)<>6 THEN BEEP:GOTO 600
605 IF D$<>"" THEN LSET DF$=D$
610 LOCATE 10,30:INPUT "",RA$
612 IF RA$<>"" AND RA$<>"LOWER WINDSOR" AND RA$<>"WINDSOR" AND RA$<>"HELLAM" AND RA$<>"HELLAM BORO" AND RA$<>"YORKANA BORO" AND RA$<>"OTHERS" THEN BEEP:GOTO 610
615 IF RA$<>"" THEN LSET RAF$=RA$
620 LOCATE 12,30:INPUT "",CD$:IF CD$<>"" AND CD$<>"F" AND CD$<>"M" THEN BEEP:GOTO 620
625 IF CD$<>"" THEN LSET CDF$=CD$
630 LOCATE 14,30:LINE INPUT "",L$:IF L$<>"" THEN LSET LF$=L$
640 LOCATE 16,30:LINE INPUT "",N$:IF N$<>"" THEN LSET NF$=N$
650 LOCATE 18,30:INPUT "",TL$:IF TL$<>"" AND LEN(TL$)<>4 THEN BEEP:GOTO 650
655 IF TL$<>"" THEN LSET TOF$=TL$
660 RETURN
670 COLOR 6:LOCATE 8,30:INPUT "",TI$:IF TI$<>"" AND LEN(TI$)<>4 THEN BEEP:GOTO 670
675 IF TI$<>"" THEN LSET TIF$=TI$
680 LOCATE 10,30:INPUT "",M$:IF M$<>"" THEN LSET MF$=M$
690 LOCATE 12,30:INPUT "",PD$:IF PD$<>"" THEN LSET PDF$=PD$
700 LOCATE 14,30:INPUT "",P$:IF P$<>"" THEN LSET PF$=P$
710 LOCATE 16,30:INPUT "",T$:IF T$<>"" THEN LSET TF$=T$
720 LOCATE 18,30:INPUT "",K$:IF K$<>"" THEN LSET KF$=K$
730 RETURN
740 KEY 1,"LIST ":KEY 2,"RUN"+CHR$(13):KEY 3,"LOAD"+CHR$(34):KEY 4,"SAVE"+CHR$(34):KEY 5,"CONT"+CHR$(13):KEY 6,","+CHR$(34)+"LPT1:"+CHR$(34)+CHR$(13)
750 RETURN
760 RESTORE 570
770 LOCATE 25,1:COLOR 12:PRINT "THIS CALL:";:COLOR 15:PRINT LOC(1);:COLOR 12:PRINT TAB(32);"TOTAL CALLS:";:COLOR 7:PRINT CINT(LOF(1)/LNGTH);:COLOR 12:PRINT TAB(69);"YEAR: ";:COLOR 8:PRINT LEFT$(FILE$,4);
780 FOR X=1 TO 8:READ FLD$:LOCATE X*2+6,1:COLOR 3:PRINT FLD$;:LOCATE X*2+7,30:COLOR 14:PRINT CHR$(24);:NEXT X
790 READ FLD$:LOCATE 8,41:COLOR 3:PRINT FLD$;:LOCATE 9,70:COLOR 14:PRINT CHR$(24);
795 LOCATE 15,40:COLOR 9:PRINT "MARK WITH <*> OR BLANK WITH <SPACE>.";:COLOR 14
800 RETURN
810 'GET 1,CN
820 LOCATE 8,30:COLOR 6:PRINT S16F$;
830 LOCATE 10,30:PRINT S21F$;
840 LOCATE 12,30:PRINT S34F$;
850 LOCATE 14,30:PRINT S35F$;
860 LOCATE 16,30:PRINT S36F$;
870 LOCATE 18,30:PRINT S41F$;
880 LOCATE 20,30:PRINT S42F$;
890 LOCATE 22,30:PRINT S44F$;
900 LOCATE 8,70:PRINT SOF$;
910 RETURN
920 COLOR 6:LOCATE 8,30:LINE INPUT "";S16$:IF S16$<>"" THEN LSET S16F$=S16$
930 LOCATE 10,30:LINE INPUT "";S21$:IF S21$<>"" THEN LSET S21F$=S21$
940 LOCATE 12,30:LINE INPUT "";S34$:IF S34$<>"" THEN LSET S34F$=S34$
950 LOCATE 14,30:LINE INPUT "";S35$:IF S35$<>"" THEN LSET S35F$=S35$
960 LOCATE 16,30:LINE INPUT "";S36$:IF S36$<>"" THEN LSET S36F$=S36$
970 LOCATE 18,30:LINE INPUT "";S41$:IF S41$<>"" THEN LSET S41F$=S41$
980 LOCATE 20,30:LINE INPUT "";S42$:IF S42$<>"" THEN LSET S42F$=S42$
990 LOCATE 22,30:LINE INPUT "";S44$:IF S44$<>"" THEN LSET S44F$=S44$
1000 LOCATE 8,70:LINE INPUT "";SO$:IF SO$<>"" THEN LSET SOF$=SO$
1010 RETURN
