10 CLEAR:SCREEN 0:WIDTH 80:VIEW PRINT:COLOR 4,0,0:KEY OFF:CLS:DIM COMMENT$(6),EQUIP$(3)
20 LNGTH=104
30 KEY(10) ON:ON KEY(10) GOSUB 10000
40 LOCATE 1,22:PRINT "LOWER WINDSOR QUARTER REPORT v1.2";
45 COLOR 9:LOCATE 2,33:PRINT "<F10> TO QUIT";
50 VIEW PRINT 3 TO 24:COLOR 7
60 INPUT "YEAR(YYYY): ",Y:IF Y<1985 THEN GOTO 60
70 FILE$=RIGHT$(STR$(Y),4)+".FCR"
80 OPEN FILE$ AS 1 LEN=LNGTH
85 IF LOF(1)=0 THEN GOSUB 10010
90 FIELD 1,6 AS DF$,13 AS RAF$,1 AS CDF$,35 AS LF$,20 AS NF$,4 AS TOF$,4 AS TIF$,2 AS MF$,6 AS PDF$,2 AS PF$,1 AS TF$,1 AS KF$,1 AS S16F$,1 AS S21F$,1 AS S34F$,1 AS S35F$,1 AS S36F$,1 AS S41F$,1 AS S42F$,1 AS S44F$,1 AS SOF$
100 INPUT "QUARTER(1-4): ",Q:IF Q<1 OR Q>4 THEN GOTO 100
110 M1=Q*3-2:M2=Q*3    'SET THE RANGE OF MONTHS
115 CN=0:MNTH=0
120 'FIND STARTING POINT
130 WHILE MNTH<M1
140 CN=CN+1
150 GET 1,CN
160 MNTH=VAL(LEFT$(DF$,2))
170 WEND
180 C1=CN
190 WHILE MNTH<M2+1 AND CN<LOF(1)/LNGTH+1
200 CN=CN+1
210 GET 1,CN
220 MNTH=VAL(LEFT$(DF$,2))
230 WEND
240 C2=CN-1
250 GOSUB 20000
260 GOSUB 30000
270 GOTO 10
10000 VIEW PRINT:CLOSE:CLS:KEY ON:RUN"MENU.BAS"
10010 LOCATE 12,1:PRINT "THERE ARE NO COMMENT/EQUIPMENT FILES!  PLEASE RUN Q-INFO.BAS.":BEEP
10020 FOR X=1 TO 10000:NEXT
10040 RETURN 10
20000 'CALCULATIONS
20005 DEF FNI(N$)=INSTR(NF$,N$)<>0:DEF FNM(S$)=(S$="*"):DEF FNO(R$)=INSTR(RAF$,R$)<>0
20010 DATA January,February,March,April,May,June,July,August
20020 DATA September,October,November,December
20025 RESTORE 20010
20030 FOR X=1 TO M2
20040 READ MNTH$
20050 NEXT X
20060 '
20070 FOR CN=C1 TO C2
20075 COLOR 7:LOCATE 12,1:PRINT "CALL NUMBER: ";CN;
20080 GET 1,CN
20090 IF LEFT$(RAF$,13)<>"LOWER WINDSOR" OR CDF$="T" THEN GOTO 20250
20095 LWT=LWT+1
20100 ' TYPE OF CALL IN TOWNSHIP
20110 IF ( FNI("STRUCTURE") OR FNI("HOUSE") ) AND FNI("FIRE") THEN TS=TS+1:GOTO 20160
20120 IF FNI("10-45") OR FNI("1045") OR FNI("10 45") THEN TA=TA+1:GOTO 20160
20130 IF ( FNI("CAR") OR FNI("VEHICLE") ) AND FNI("FIRE") THEN TV=TV+1:GOTO 20160
20140 IF ( ( FNI("BRUSH") OR FNI("WOOD") ) AND FNI("FIRE") ) OR FNI("BURN") THEN TB=TB+1:GOTO 20160
20150 TOT=TOT+1                                             
20160 ' CALLS REQUIRING MUTUAL AID INTO L.W.
20170 S16=S16-FNM(S16F$)
20180 S21=S21-FNM(S21F$)
20190 S34=S34-FNM(S34F$)
20200 S35=S35-FNM(S35F$)
20210 S36=S36-FNM(S36F$)
20220 S41=S41-FNM(S41F$)
20230 SOT=SOT-FNM(SOF$)
20240 GOTO 20300
20250 ' CALLS OUTSIDE L.W.
20255 IF CDF$="T" THEN GOTO 20300
20260 OW=OW-(FNO("WINDSOR") AND NOT(FNO("LOWER WINDSOR")))
20270 OH=OH-FNO("HELLAM")
20280 OY=OY-FNO("YORKANA")
20290 OO=OO-FNO("OTHERS")
20295 '
20300 NEXT CN
20305 CLOSE 1
20310 ' OPEN FILES WITH EXTRA INFO
20315 ON ERROR GOTO 10010
20320 OPEN LEFT$(FILE$,4)+"-"+RIGHT$(STR$(Q),1)+".CMT" FOR INPUT AS 1
20330 L=0:WHILE NOT(EOF(1))
20340 L=L+1
20350 LINE INPUT #1,COMMENT$(L)
20360 WEND
20370 CLOSE 1
20380 OPEN LEFT$(FILE$,4)+"-"+RIGHT$(STR$(Q),1)+".EQT" FOR INPUT AS 1
20390 L=0:WHILE NOT(EOF(1))
20400 L=L+1
20410 LINE INPUT #1,EQUIP$(L)
20420 WEND
20430 CLOSE 1
20440 RETURN
30000 'PRINT FORM
30005 BEGIN$=CHR$(27)+"-"+CHR$(1):QUIT$=CHR$(27)+"-"+CHR$(0)
30010 LPRINT CHR$(27);"X";CHR$(10);CHR$(70);    'SET MARGINS
30020 FOR X=1 TO 3:LPRINT:NEXT
30030 LPRINT "TO:  BOARD OF SUPERVISORS"
30040 LPRINT "     LOWER WINDSOR TOWNSHIP"
30050 LPRINT:LPRINT
30052 LPRINT CHR$(27);"G";              'BOLD ON
30055 LPRINT TAB(15);"QUARTERLY FIRE COMPANY REPORT";
30056 LPRINT CHR$(27);"H"               'BOLD OFF
30057 LPRINT:LPRINT
30060 LPRINT "YEAR  ";BEGIN$;Y;" ";QUIT$
30065 LPRINT "QUARTER  ";BEGIN$;STR$(Q);"  ";QUIT$
30070 'LPRINT "MONTH ENDING  ";BEGIN$;" ";MNTH$;SPACE$(16-LEN(MNTH$));QUIT$
30080 LPRINT "FIRE COMPANY  ";BEGIN$;"Yorkana Fire Company   ";QUIT$
30090 LPRINT "SUBMITTED BY  ";BEGIN$;"Larry M. Flinchbaugh   ";QUIT$;  '    ** "   DATE  ";BEGIN$;DATE$;QUIT$
30100 LPRINT:LPRINT
30110 LPRINT "NUMBER OF CALLS IN LOWER WINDSOR TOWNSHIP  ";BEGIN$;
30120 LPRINT USING "  ##  ";LWT;
30130 LPRINT QUIT$
30140 LPRINT
30150 LPRINT CHR$(9);"TYPE OF CALLS:"
30160 LPRINT CHR$(9);CHR$(9);"Structure";CHR$(9);CHR$(9);BEGIN$;
30170 LPRINT USING "  ##  ";TS;
30180 LPRINT QUIT$
30190 LPRINT CHR$(9);CHR$(9);"Vehicle Accidents";CHR$(9);BEGIN$;
30200 LPRINT USING "  ##  ";TA;
30210 LPRINT QUIT$
30220 LPRINT CHR$(9);CHR$(9);"Vehicle Fires";CHR$(9);CHR$(9);BEGIN$;
30230 LPRINT USING "  ##  ";TV;
30240 LPRINT QUIT$
30250 LPRINT CHR$(9);CHR$(9);"Brush/Wood Fires";CHR$(9);BEGIN$;
30260 LPRINT USING "  ##  ";TB;
30270 LPRINT QUIT$
30280 LPRINT CHR$(9);CHR$(9);"Others";CHR$(9);CHR$(9);CHR$(9);BEGIN$;
30290 LPRINT USING "  ##  ";TOT;
30300 LPRINT QUIT$
30310 LPRINT:LPRINT
30320 LPRINT "NUMBER OF CALLS OUTSIDE LOWER WINDSOR TOWNSHIP"
30330 LPRINT
30340 LPRINT CHR$(9);"NUMBER";CHR$(9);CHR$(9);"MUNICIPALITY"
30350 LPRINT CHR$(9);BEGIN$;
30360 LPRINT USING "  ##  ";OW;
30370 LPRINT QUIT$;CHR$(9);CHR$(9);BEGIN$;"Windsor                        ";QUIT$
30380 LPRINT CHR$(9);BEGIN$;
30390 LPRINT USING "  ##  ";OH;
30400 LPRINT QUIT$;CHR$(9);CHR$(9);BEGIN$;"Hellam                         ";QUIT$
30410 LPRINT CHR$(9);BEGIN$;
30420 LPRINT USING "  ##  ";OY;
30430 LPRINT QUIT$;CHR$(9);CHR$(9);BEGIN$;"Yorkana Borough                ";QUIT$
30440 LPRINT CHR$(9);BEGIN$;
30450 LPRINT USING "  ##  ";OO;
30460 LPRINT QUIT$;CHR$(9);CHR$(9);BEGIN$;"Others                         ";QUIT$
30470 LPRINT:LPRINT
30480 LPRINT "PURCHASES OF EQUIPMENT:"
30490 FOR L=1 TO 3
30500 LPRINT BEGIN$;EQUIP$(L);QUIT$
30510 NEXT L
30520 LPRINT
30530 LPRINT "COMMENTS:";
30532 IF S16=0 AND S21=0 AND S34=0 AND S35=0 AND S36=0 AND S41=0 AND SOT=0 THEN GOTO 30770
30535 LPRINT CHR$(9);"LOWER WINDSOR CALLS REQUIRING MUTUAL AID"
30540 LPRINT
30550 LPRINT CHR$(9);CHR$(9);"NUMBER";CHR$(9);CHR$(9);"COMPANY"
30555 IF S16=0 THEN GOTO 30585
30560 LPRINT CHR$(9);CHR$(9);BEGIN$;
30570 LPRINT USING "  ##  ";S16;
30580 LPRINT QUIT$;CHR$(9);CHR$(9);BEGIN$;"Springettsbury         ";QUIT$
30585 IF S21=0 THEN GOTO 30615
30590 LPRINT CHR$(9);CHR$(9);BEGIN$;
30600 LPRINT USING "  ##  ";S21;
30610 LPRINT QUIT$;CHR$(9);CHR$(9);BEGIN$;"Hellam                  ";QUIT$
30615 IF S34=0 THEN GOTO 30645
30620 LPRINT CHR$(9);CHR$(9);BEGIN$;
30630 LPRINT USING "  ##  ";S34;
30640 LPRINT QUIT$;CHR$(9);CHR$(9);BEGIN$;"Red Lion               ";QUIT$
30645 IF S35=0 THEN GOTO 30675
30650 LPRINT CHR$(9);CHR$(9);BEGIN$;
30660 LPRINT USING "  ##  ";S35;
30670 LPRINT QUIT$;CHR$(9);CHR$(9);BEGIN$;"Dallastown             ";QUIT$
30675 IF S36=0 THEN GOTO 30705
30680 LPRINT CHR$(9);CHR$(9);BEGIN$;
30690 LPRINT USING "  ##  ";S36;
30700 LPRINT QUIT$;CHR$(9);CHR$(9);BEGIN$;"Yoe                    ";QUIT$
30705 IF S41=0 THEN GOTO 30735
30710 LPRINT CHR$(9);CHR$(9);BEGIN$;
30720 LPRINT USING "  ##  ";S41;
30730 LPRINT QUIT$;CHR$(9);CHR$(9);BEGIN$;"Wrightsville           ";QUIT$
30735 IF SOT=0 THEN GOTO 30770
30740 LPRINT CHR$(9);CHR$(9);BEGIN$;
30750 LPRINT USING "  ##  ";SOT;
30760 LPRINT QUIT$;CHR$(9);CHR$(9);BEGIN$;"Others                 ";QUIT$
30770 LPRINT
30780 FOR L=1 TO 4
30790 LPRINT BEGIN$;COMMENT$(L);QUIT$
30800 NEXT L
30810 LPRINT CHR$(27);"X";CHR$(1);CHR$(80);             'RESET MARGINS
30820 LPRINT CHR$(12);                          'FORM FEED
30830 RETURN
