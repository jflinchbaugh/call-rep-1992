10 CLEAR:SCREEN 0:WIDTH 80:VIEW PRINT:COLOR 4,0,0:KEY OFF:CLS:DIM COMMENT$(4),EQUIP$(3)
30 KEY(10) ON:ON KEY(10) GOSUB 10000
40 LOCATE 1,24,0:PRINT "QUARTERLY INFORMATION ENTRY v1";
45 COLOR 9:LOCATE 2,33:PRINT "<F10> TO QUIT";
50 VIEW PRINT 3 TO 24:COLOR 7
60 INPUT "YEAR(YYYY): ",Y:IF Y<1985 THEN GOTO 60
100 INPUT "QUARTER(1-4): ",Q:IF Q<1 OR Q>4 THEN GOTO 100
105 ON ERROR GOTO 10010
110 FILE$=RIGHT$(STR$(Y),4)+"-"+RIGHT$(STR$(Q),1)
120 OPEN FILE$+".CMT" FOR INPUT AS 1
130 L=0:WHILE NOT(EOF(1))
140 L=L+1
150 LINE INPUT #1,COMMENT$(L)
160 WEND
170 CLOSE 1
180 OPEN FILE$+".EQT" FOR INPUT AS 1
190 L=0:WHILE NOT(EOF(1))
200 L=L+1
210 LINE INPUT #1,EQUIP$(L)
220 WEND
230 CLOSE 1
240 'PRINT OLD ONE
250 CLS:LOCATE 4,34:COLOR 2:PRINT "Edit COMMENT";
260 FOR L=1 TO 4
270 COLOR 0,7:LOCATE L+5,1:PRINT COMMENT$(L);
280 NEXT L
290 COLOR 2,0:LOCATE 12,1:PRINT "Enter nothing on each line to make no changes. Do not exceed the provided field."
300 FOR L=1 TO 4
310 COLOR 0,7:LOCATE 13+L,1:PRINT SPACE$(55);
320 NEXT L
330 FOR L=1 TO 4
340 COLOR 0,7:LOCATE 13+L,1:LINE INPUT "";A$
350 IF A$<>"" THEN COMMENT$(L)=LEFT$(A$,55)+SPACE$(55-LEN(A$)) ELSE IF COMMENT$(L)="" THEN COMMENT$(L)=SPACE$(55)
360 COLOR 7,0:LOCATE 13+L,56:PRINT SPACE$(9)
370 NEXT L
380 OPEN FILE$+".CMT" FOR OUTPUT AS 1
390 FOR L=1 TO 4
400 PRINT #1,COMMENT$(L)
410 NEXT L
420 CLOSE 1
430 'EQUIPMENT
440 CLS:LOCATE 4,33:COLOR 2:PRINT "Edit EQUIPMENT";
450 FOR L=1 TO 3
460 COLOR 0,7:LOCATE L+5,1:PRINT EQUIP$(L);
470 NEXT L
480 COLOR 2,0:LOCATE 12,1:PRINT "Enter nothing on each line to make no changes. Do not exceed the provided field."
490 FOR L=1 TO 3
500 COLOR 0,7:LOCATE 13+L,1:PRINT SPACE$(55);
510 NEXT L
520 FOR L=1 TO 3
530 COLOR 0,7:LOCATE 13+L,1:LINE INPUT "";A$
540 IF A$<>"" THEN EQUIP$(L)=LEFT$(A$,55)+SPACE$(55-LEN(A$)) ELSE IF EQUIP$(L)="" THEN EQUIP$(L)=SPACE$(55)
550 COLOR 7,0:LOCATE 13+L,56:PRINT SPACE$(9)
560 NEXT L
570 OPEN FILE$+".EQT" FOR OUTPUT AS 1
580 FOR L=1 TO 3
590 PRINT #1,EQUIP$(L)
600 NEXT L
610 CLOSE 1
620 GOTO 10
10000 VIEW PRINT:CLOSE:CLS:KEY ON:RUN"MENU.BAS"
10010 IF ERR<>53 THEN GOTO 10030
10022 IF ERL=120 THEN RESUME 180
10025 IF ERL=180 THEN RESUME 240
10030 FOR X=0 TO 3:PRINT EXTERR(X),:NEXT X:PRINT ERL,ERR;:STOP
