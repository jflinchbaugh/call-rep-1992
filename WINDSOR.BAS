10 CLEAR:SCREEN 0:WIDTH 80:VIEW PRINT:COLOR 4,0,0:KEY OFF:CLS
20 LNGTH=104
30 KEY(10) ON:ON KEY(10) GOSUB 10000
40 LOCATE 1,27:PRINT "WINDSOR QUARTER REPORT v1.2";
45 COLOR 9:LOCATE 2,33:PRINT "<F10> TO QUIT";
50 VIEW PRINT 3 TO 24:COLOR 7
60 INPUT "YEAR(YYYY): ",Y:IF Y<1985 THEN GOTO 60
70 FILE$=RIGHT$(STR$(Y),4)+".FCR"
80 OPEN FILE$ AS 1 LEN=LNGTH
85 IF LOF(1)=0 THEN GOSUB 10010
90 FIELD 1,6 AS DF$,13 AS RAF$,1 AS CDF$,35 AS LF$,20 AS NF$,4 AS TOF$,4 AS TIF$,2 AS MF$,6 AS PDF$,2 AS PF$,1 AS TF$,1 AS KF$,1 AS S16F$,1 AS S21F$,1 AS S34F$,1 AS S35F$,1 AS S36F$,1 AS S41F$,1 AS S42F$,1 AS S44F$,1 AS SOF$
100 INPUT "QUARTER(1-4): ",Q:IF Q<1 OR Q>4 THEN GOTO 100
110 M1=Q*3-2:M2=Q*3    'SET THE RANGE OF MONTHS
115 CN=0:MNTH=0
120 'FIND STARTING POINT
130 WHILE MNTH<M1
140 CN=CN+1
150 GET 1,CN
160 MNTH=VAL(LEFT$(DF$,2))
170 WEND
180 C1=CN
190 WHILE MNTH<M2+1 AND CN<LOF(1)/LNGTH+1
200 CN=CN+1
210 GET 1,CN
220 MNTH=VAL(LEFT$(DF$,2))
230 WEND
240 C2=CN-1
400 GOSUB 20000     'HEADER
410 GOSUB 30000      'INPUT DATA AND PRINT IT
413 GOSUB 35000         'FILL EMPTY SPACE
417 GOSUB 40000         'RESET PRINTER
420 GOTO 10
10000 VIEW PRINT:CLOSE:CLS:KEY ON:RUN"MENU.BAS"
10010 PRINT "THERE ARE NO CALLS IN THIS FILE!":BEEP
10020 FOR X=1 TO 5000:NEXT
10030 CLOSE:KILL FILE$
10040 RETURN 10
20000 'SETUP PRINTER/HEADER
20020 WIDTH "LPT1:",137
20030 LPRINT CHR$(18);    'SET STANDARD PRINT
20040 LPRINT CHR$(14);    'DOUBLE WIDTH MODE
20045 '
20050 LPRINT SPC(15);"YORKANA 38"
20055 '
20060 LPRINT CHR$(20);    'CANCEL DOUBLE WIDTH PRINTING
20080 LPRINT CHR$(27);CHR$(53);CHR$(0);     'SET CR ONLY
20090 LPRINT CHR$(27);CHR$(70);    'CANCEL EMPHASIZED PRINTING
20100 LPRINT CHR$(27);CHR$(45);CHR$(0);    'CANCEL UNDERLINING
20105 '
20110 LPRINT SPC(16);"WINDSOR TOWNSHIP EMERGENCY CALL RESPONSE REPORT"
20120 LPRINT SPC(27);"YEAR";Y;"QUARTER";Q;"REPORT"
20130 LPRINT SPC(14);"SUBMITTED BY LARRY M. FLINCHBAUGH, ASS'T CHIEF 38-2"
20140 '
20150 LPRINT CHR$(15);    'SET CONDENSED PRINTING
20160 LPRINT:LPRINT
20170 LPRINT TAB(2);"Date";TAB(14);"Location";TAB(52);"Nature";TAB(75);"Out";TAB(87);"In";TAB(97);"Miles";TAB(107);"Damage";TAB(118);"Personnel";TAB(132);"Trucks"
20175 LPRINT
20180 RETURN
30000 'PRINT DATA FROM FILES
30005 NUM=0
30010 LPRINT CHR$(15);      'CONDENSED PRINTING
30020 FOR CN=C1 TO C2
30030 GET 1,CN
30040 IF LEFT$(RAF$,7)<>"WINDSOR" OR LEFT$(CDF$,1)="T" THEN GOTO 30160
30050 D$=LEFT$(DF$,2)+"/"+MID$(DF$,3,2)+"/"+RIGHT$(DF$,2)
30060 L$=LF$
30070 N$=NF$
30080 TL$=LEFT$(TOF$,2)+":"+RIGHT$(TOF$,2)
30090 TI$=LEFT$(TIF$,2)+":"+RIGHT$(TIF$,2)
30100 M$=MF$
30110 PD$=PDF$
30120 P$=PF$
30130 T$=TF$
30140 LPRINT TAB(2);D$;TAB(14);L$;TAB(52);N$;TAB(75);TL$;TAB(87);TI$;TAB(97);M$;TAB(107);PD$;TAB(118);P$;TAB(132);T$
30150 LPRINT:LPRINT
30155 NUM=NUM+1
30160 NEXT CN
30170 RETURN
35000 FOR LIN=1 TO (18-NUM)
35010 LPRINT TAB(2);STRING$(8,46);TAB(14);STRING$(35,46);TAB(52);STRING$(20,46);TAB(75);STRING$(5,46);TAB(87);STRING$(5,46);TAB(97);STRING$(2,46);TAB(107);STRING$(6,46);TAB(117);STRING$(2,46);TAB(132);STRING$(1,46)     'PRINT FILLER  "."
35020 LPRINT:LPRINT
35030 NEXT LIN
35040 LPRINT CHR$(12);
35050 RETURN
40000 'PRINTER RESET
40010 LPRINT CHR$(18);    'STANDARD PRINT SIZE
40020 WIDTH "LPT1:",80
40030 RETURN
