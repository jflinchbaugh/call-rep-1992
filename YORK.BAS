10 CLOSE:CLEAR:SCREEN 0:WIDTH 80:VIEW PRINT:COLOR 4,0,0:KEY OFF:CLS
20 LNGTH=104
30 KEY(10) ON:ON KEY(10) GOSUB 270
40 LOCATE 1,24:PRINT "YORK COUNTY QUARTER REPORT v1.7";
50 COLOR 9:LOCATE 2,33:PRINT "<F10> TO QUIT";
60 VIEW PRINT 3 TO 24:COLOR 7
70 INPUT "YEAR(YYYY): ",Y:IF Y<1985 THEN GOTO 70
80 FILE$=RIGHT$(STR$(Y),4)+".FCR"
90 OPEN FILE$ AS 1 LEN=LNGTH
100 IF LOF(1)=0 THEN GOTO 280
110 FIELD 1,6 AS DF$,13 AS RAF$,1 AS CDF$,35 AS LF$,20 AS NF$,4 AS TOF$,4 AS TIF$,2 AS MF$,6 AS PDF$,2 AS PF$,1 AS TF$,1 AS KF$,1 AS S16F$,1 AS S21F$,1 AS S34F$,1 AS S35F$,1 AS S36F$,1 AS S41F$,1 AS S42F$,1 AS S44F$,1 AS SOF$
120 INPUT "QUARTER(1-4): ",Q:IF Q<1 OR Q>4 THEN GOTO 120
130 M1=Q*3-2:M2=Q*3        'SET THE RANGE OF THE MONTHS
140 CN=0
150 'READ PREVIOUS AND CURRENT QUARTERS UPTO THIS QUARTER
160 COLOR 15
170 CN=CN+1
180 LOCATE 12,1,0:PRINT "CALL NUMBER:";CN;"  ";
190 GET 1,CN
200 MNTH=VAL(LEFT$(DF$,2))
210 IF MNTH>0 AND MNTH<M1 THEN GOSUB 350                         'PREV QUARTERS
220 IF MNTH>=M1 AND MNTH<=M2 THEN GOSUB 600        'THIS QUARTER
230 IF (MNTH<1 OR MNTH>12) AND CN<=LOF(1)/LNGTH THEN GOTO 320
240 GOSUB 850          'final calculations
250 GOSUB 1060     'print
260 GOTO 10
270 CLEAR:VIEW PRINT:CLOSE:CLS:KEY ON:RUN"MENU.BAS"
280 PRINT "THERE ARE NO CALLS IN THIS FILE!":BEEP
290 FOR X=1 TO 5000:NEXT
300 CLOSE:KILL FILE$
310 RETURN 10
320 PRINT "FAULT IN CALL NUMBER";CN;"   <ENTER>";
330 WHILE INKEY$<>CHR$(13):WEND
340 GOTO 270
350 IF LEFT$(RAF$,13)<>"LOWER WINDSOR" THEN GOTO 380
360 IF CDF$="F" THEN PLF=PLF+1
370 IF CDF$="M" THEN PLM=PLM+1
380 IF LEFT$(RAF$,7)<>"WINDSOR" THEN GOTO 410
390 IF CDF$="F" THEN PWF=PWF+1
400 IF CDF$="M" THEN PWM=PWM+1
410 IF LEFT$(RAF$,12)<>"YORKANA BORO" THEN GOTO 430
420 PYF=PYF+1
430 IF LEFT$(RAF$,6)<>"HELLAM" THEN GOTO 460     'HELLAM TWP. OF BORO
440 IF CDF$="F" THEN PHF=PHF+1
450 IF CDF$="M" THEN PHM=PHM+1
460 IF LEFT$(RAF$,6)<>"OTHERS" THEN GOTO 480
470 IF CDF$="M" THEN POM=POM+1
480 PPD=PPD+VAL(PDF$)
490 PTP=PTP+VAL(PF$)
500 PTD=PTD+VAL(KF$)
510 PTM=PTM+VAL(MF$)
520 PTIH=VAL(LEFT$(TIF$,2)):PTOH=VAL(LEFT$(TOF$,2))
530 PTIM=VAL(RIGHT$(TIF$,2)):PTOM=VAL(RIGHT$(TOF$,2))
540 PTMH=PTIH-PTOH:IF PTMH<0 THEN PTMH=PTMH+24
550 PTMM=PTIM-PTOM:IF PTMM<0 THEN PTMM=PTMM+60:PTMH=PTMH-1
560 PTMH=PTMH+PTMM/60
570 PTTH=PTTH+PTMH
580 PMH=PMH+(VAL(PF$)*PTMH)
590 RETURN 170
600 IF LEFT$(RAF$,13)<>"LOWER WINDSOR" THEN GOTO 630
610 IF CDF$="F" THEN LF1=LF1+1
620 IF CDF$="M" THEN LM1=LM1+1
630 IF LEFT$(RAF$,7)<>"WINDSOR" THEN GOTO 660
640 IF CDF$="F" THEN WF1=WF1+1
650 IF CDF$="M" THEN WM1=WM1+1
660 IF LEFT$(RAF$,12)<>"YORKANA BORO" THEN GOTO 680
670 YF1=YF1+1
680 IF LEFT$(RAF$,6)<>"HELLAM" THEN GOTO 710     'HELLAM TWP. OR BORO
690 IF CDF$="F" THEN HF1=HF1+1
700 IF CDF$="M" THEN HM1=HM1+1
710 IF LEFT$(RAF$,6)<>"OTHERS" THEN GOTO 730
720 IF CDF$="M" THEN OM1=OM1+1
730 PD1=PD1+VAL(PDF$)
740 TP1=TP1+VAL(PF$)
750 TD1=TD1+VAL(KF$)
760 TM1=TM1+VAL(MF$)
770 TIH1=VAL(LEFT$(TIF$,2)):TOH1=VAL(LEFT$(TOF$,2))
780 TIM1=VAL(RIGHT$(TIF$,2)):TOM1=VAL(RIGHT$(TOF$,2))
790 TMH1=TIH1-TOH1:IF TMH1<0 THEN TMH1=TMH1+24
800 TMM1=TIM1-TOM1:IF TMM1<0 THEN TMM1=TMM1+60:TMH1=TMH1-1
810 TMH1=TMH1+TMM1/60
820 TTH1=TTH1+TMH1
830 MH1=MH1+(VAL(PF$)*TMH1)
840 RETURN 170
850 PTC=PLF+PLM+PWF+PWM+PYF+PHF+PHM+POM
860 IF PTC>0 THEN PAR=PTP/PTC
870 TC1=LF1+LM1+WF1+WM1+YF1+HF1+HM1+OM1
880 IF TC1>0 THEN AR1=TP1/TC1
890 '
900 YLF=PLF+LF1
910 YLM=PLM+LM1
920 YWF=PWF+WF1
930 YWM=PWM+WM1
940 YYF=PYF+YF1
950 YHF=PHF+HF1
960 YHM=PHM+HM1
970 YOM=POM+OM1
980 '
990 YTH=PTTH+TTH1
1000 YPD=PPD+PD1
1010 YTC=PTC+TC1
1020 YTP=PTP+TP1
1030 YMH=PMH+MH1
1040 IF YTC>0 THEN YAR=YTP/YTC
1050 YTD=PTD+TD1
1060 ON ERROR GOTO 1840
1070 LPRINT CHR$(27);"G";       'SET BOLD PRINT
1080 FOR X=1 TO 4:LPRINT:NEXT X
1090 LPRINT SPC(25);"YORK COUNTY FIRE AND EMERGENCY"
1100 LPRINT
1110 LPRINT SPC(36);"INCIDENT"
1120 LPRINT
1130 LPRINT SPC(37);"REPORT"
1140 LPRINT
1150 LPRINT SPC(26);"Year ";CHR$(27);"-";CHR$(1);Y;CHR$(27);"-";CHR$(0);" Quarter Number ";CHR$(27);"-";CHR$(1);Q;CHR$(27);"-";CHR$(0)
1160 LPRINT CHR$(27);"H";
1170 LPRINT
1180 LPRINT SPC(18);"YORKANA COMMUNITY VOLUNTEER FIRE COMPANY"
1190 LPRINT
1200 LPRINT SPC(26);"RD#9 BOX 246 YORK PA. 17402"
1210 LPRINT
1220 LPRINT SPC(34);"717-757-2455"
1230 LPRINT CHR$(27);"X";CHR$(9);CHR$(80)   'SET MARGINS
1240 LPRINT "Municipality";SPC(26);CHR$(27);"-";CHR$(1);SPC(6);"Number of Calls";SPC(9);CHR$(27);"-";CHR$(0)
1250 LPRINT SPC(38);"| Previous | This    | Total |"
1260 LPRINT SPC(38);CHR$(27);"-";CHR$(1);"| Quarters | Quarter | Year  |";CHR$(27);"-";CHR$(0)
1270 LPRINT SPC(38);"|          |         |       |"
1280 LPRINT "  LOWER WINDSOR TWP. ---- First Due   ";CHR$(27);"-";CHR$(1);
1290 LPRINT USING "|   ###    |   ###   |  ###  |";PLF,LF1,YLF;:LPRINT CHR$(27);"-";CHR$(0)
1300 LPRINT SPC(38);"|          |         |       |"
1310 LPRINT SPC(26);"Mutual Aid  ";CHR$(27);"-";CHR$(1);
1320 LPRINT USING "|   ###    |   ###   |  ###  |";PLM,LM1,YLM;:LPRINT CHR$(27);"-";CHR$(0)
1330 LPRINT SPC(38);"|          |         |       |"
1340 LPRINT "  WINDSOR TWP. ---------- First Due   ";CHR$(27);"-";CHR$(1);
1350 LPRINT USING "|   ###    |   ###   |  ###  |";PWF,WF1,YWF;:LPRINT CHR$(27);"-";CHR$(0)
1360 LPRINT SPC(38);"|          |         |       |"
1370 LPRINT SPC(26);"Mutual Aid  ";CHR$(27);"-";CHR$(1);
1380 LPRINT USING "|   ###    |   ###   |  ###  |";PWM,WM1,YWM;:LPRINT CHR$(27);"-";CHR$(0)
1390 LPRINT SPC(38);"|          |         |       |"
1400 LPRINT "  HELLAM TWP. ----------- First Due   ";CHR$(27);"-";CHR$(1);
1410 LPRINT USING "|   ###    |   ###   |  ###  |";PHF,HF1,YHF;:LPRINT CHR$(27);"-";CHR$(0)
1420 LPRINT SPC(38);"|          |         |       |"
1430 LPRINT SPC(26);"Mutual Aid  ";CHR$(27);"-";CHR$(1);
1440 LPRINT USING "|   ###    |   ###   |  ###  |";PHM,HM1,YHM;:LPRINT CHR$(27);"-";CHR$(0)
1450 LPRINT SPC(38);"|          |         |       |"
1460 LPRINT "  YORKANA BORO. --------- First Due   ";CHR$(27);"-";CHR$(1);
1470 LPRINT USING "|   ###    |   ###   |  ###  |";PYF,YF1,YYF;:LPRINT CHR$(27);"-";CHR$(0)
1480 LPRINT SPC(38);"|          |         |       |"
1490 LPRINT "  OTHERS ---------------- Mutual Aid  ";CHR$(27);"-";CHR$(1);
1500 LPRINT USING "|   ###    |   ###   |  ###  |";POM,OM1,YOM;:LPRINT CHR$(27);"-";CHR$(0)
1510 LPRINT SPC(38);"|          |         |       |"
1520 LPRINT "Total Number of Calls Responded to    ";CHR$(27);"-";CHR$(1);
1530 LPRINT USING "|   ###    |   ###   |  ###  |";PTC,TC1,YTC;:LPRINT CHR$(27);"-";CHR$(0)
1540 LPRINT SPC(38);CHR$(27);"-";CHR$(1);SPC(30);CHR$(27);"-";CHR$(0)
1550 LPRINT SPC(38);"|          |         |       |"
1560 LPRINT "Total Property Damage                 ";CHR$(27);"-";CHR$(1);
1570 LPRINT USING "|$#########|$########|$######|";PPD,PD1,YPD;:LPRINT CHR$(27);"-";CHR$(0)
1580 LPRINT
1590 LPRINT "Number of Personnel Hours on Call     ";CHR$(27);"-";CHR$(1);SPC(30);CHR$(27);"-";CHR$(0)
1600 LPRINT SPC(38);"|          |         |       |"
1610 LPRINT "  Total Hours on Call                 ";CHR$(27);"-";CHR$(1);
1620 LPRINT USING "|  ####.## | ###.##  |####.##|";PTTH,TTH1,YTH;:LPRINT CHR$(27);"-";CHR$(0)
1630 LPRINT SPC(38);"|          |         |       |"
1640 LPRINT "  Total Manhours on Call              ";CHR$(27);"-";CHR$(1);
1650 LPRINT USING "| ######.##| #####.##|#######|";PMH,MH1,YMH;:LPRINT CHR$(27);"-";CHR$(0)
1660 LPRINT SPC(38);"|          |         |       |"
1670 LPRINT "  Total Number of Personnel Responded ";CHR$(27);"-";CHR$(1);
1680 LPRINT USING "|   ####   |   ###   |  #### |";PTP,TP1,YTP;:LPRINT CHR$(27);"-";CHR$(0)
1690 LPRINT SPC(38);"|          |         |       |"
1700 LPRINT "  Average Number of Personnel Per Call";CHR$(27);"-";CHR$(1);
1710 LPRINT USING "|   ##.##  |  ##.##  | ##.## |";PAR,AR1,YAR;:LPRINT CHR$(27);"-";CHR$(0)
1720 LPRINT SPC(38);"|          |         |       |"
1730 LPRINT "  Total Number of Deaths              ";CHR$(27);"-";CHR$(1);
1740 LPRINT USING "|    ##    |    #    |   ##  |";PTD,TD1,YTD;:LPRINT CHR$(27);"-";CHR$(0)
1750 LPRINT
1760 LPRINT CHR$(27);"X";CHR$(1);CHR$(80);
1770 LPRINT SPC(26);"Larry Flinchbaugh"
1780 LPRINT SPC(26);"Chief 38-2"
1790 LPRINT SPC(26);"RD 12 BOX 522 YORK, PA. 17406"
1800 LPRINT SPC(26);"717-252-3968"
1810 LPRINT CHR$(27);"X";CHR$(1);CHR$(80);
1820 LPRINT CHR$(12);
1830 RETURN 10
1840 IF ERR=11 THEN RESUME
1850 IF ERR=25 THEN COLOR 38:LOCATE 10,1:PRINT "PRINTER MAY BE OFF-LINE!";:COLOR 15:PRINT "<ENTER>";
1860 WHILE INKEY$<>CHR$(13):WEND
1870 RESUME
